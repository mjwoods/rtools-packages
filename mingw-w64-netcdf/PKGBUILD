# Contributor: Robert T. McGibbon <rmcgibbo@gmail.com>

_realname=netcdf
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.8.0
pkgrel=9000
pkgdesc="Interface for scientific data access to large binary data (mingw-w64)"
arch=('any')
license=('custom')
url="https://www.unidata.ucar.edu/software/netcdf/"
depends=("${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-hdf5")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake")
source=("bare-clones/netcdf-c::git+https://github.com/Unidata/netcdf-c.git#commit=f676bb3575ae4187758bad84804a16315f9da20a"
        0001-mingw-cmake.patch
        0003-ncconfigure-strlcat-export.patch
        0006-cvt-conversion.patch
	0007-mingw-config.patch
        0013-mingw-path.patch)
sha256sums=('SKIP'
            'a306c20f6593d314701147323f952d4df80e77174c45ab201949f402d191e221'
            '0116525b0b513b91c22519c38d9010a4eff0a7319cc2a3d882b296389119b632'
            'f5f5291c2486e9e765a93975d99871cee584bdd3d64e16745982aadeb799f00c'
            'a1aa112779a83ec74acd7de8a0a57915f5d37cae44633d0a4d0d20784a2d288c'
            '28c7a821c52539c489ee91525d9388af17ce07bf11e4442ac12bf83c34830cb8')
prepare() {
  cd "${srcdir}/${_realname}-c"
  patch -p1 -i ${srcdir}/0001-mingw-cmake.patch
  patch -p1 -i ${srcdir}/0003-ncconfigure-strlcat-export.patch
  patch -p1 -i ${srcdir}/0006-cvt-conversion.patch
  patch -p1 -i ${srcdir}/0007-mingw-config.patch
  patch -p1 -i ${srcdir}/0013-mingw-path.patch
}

build() {
  [[ -d ${srcdir}/build-static-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-static-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-static-${MINGW_CHOST} && cd ${srcdir}/build-static-${MINGW_CHOST}

  if [[ $RUN_CHECK = 'y' ]]; then
    ENABLE_TESTS=ON
    LINKER_FLAGS=-Wl,--allow-multiple-definition
  else
    ENABLE_TESTS=OFF
    LINKER_FLAGS=
  fi

  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G "MSYS Makefiles" \
    -DNC_EXTRA_DEPS="-lhdf5 -lcurl -lssh2 -lssl -lcrypto -lz -lws2_32 -lcrypt32 -lwldap32" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_PREFIX_PATH="${PREFIX_WIN};${PREFIX_WIN}/${MINGW_CHOST}" \
    -DCMAKE_EXE_LINKER_FLAGS="${LINKER_FLAGS}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_FLAGS_RELEASE="-O3 -fno-optimize-sibling-calls -DNDEBUG -DWINPATH" \
    -DBUILD_SHARED_LIBS=OFF \
    -DENABLE_DLL=OFF \
    -DENABLE_DAP=ON \
    -DENABLE_TESTS=${ENABLE_TESTS} \
    -DENABLE_NETCDF_4=ON \
    "${srcdir}/${_realname}-c"
  make
}

check() {
  cd ${srcdir}/build-static-${MINGW_CHOST}
  make test
}

package() {
  cd ${srcdir}/build-static-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${_realname}-c/COPYRIGHT \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYRIGHT

  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  for _f in "${pkgdir}${MINGW_PREFIX}"/lib/cmake/netCDF/*.cmake; do
    sed -e "s|${PREFIX_WIN}|\$\{_IMPORT_PREFIX\}|g" -i ${_f}
  done

  for pcfile in "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/*.pc; do
    sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i "${pcfile}"
  done

  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i "${pkgdir}${MINGW_PREFIX}/bin/nc-config"
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i "${pkgdir}${MINGW_PREFIX}/lib/libnetcdf.settings"
}
