# Contributor: Robert T. McGibbon <rmcgibbo@gmail.com>

_realname=netcdf
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.8.1
pkgrel=0
pkgdesc="Interface for scientific data access to large binary data (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
license=('custom')
url="https://www.unidata.ucar.edu/software/netcdf/"
depends=("${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-hdf5")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake")
source=("bare-clones/netcdf-c-${pkgver}::git+https://github.com/mjwoods/netcdf-c.git#branch=4.8.1-mingw"
         0012-mingw-pwd.patch)
#        0001-mingw-cmake.patch
#        0003-ncconfigure-strlcat-export.patch
#        0006-cvt-conversion.patch
#        0007-mingw-config.patch)
sha256sums=('SKIP'
            'ec69711531f9cd1707b9fde3a683363dbb319a1203f6d66870c37b303dd67c76')
#            'a306c20f6593d314701147323f952d4df80e77174c45ab201949f402d191e221'
#            '0116525b0b513b91c22519c38d9010a4eff0a7319cc2a3d882b296389119b632'
#            'f5f5291c2486e9e765a93975d99871cee584bdd3d64e16745982aadeb799f00c'
#            'a1aa112779a83ec74acd7de8a0a57915f5d37cae44633d0a4d0d20784a2d288c')

prepare() {
  cd "${srcdir}/${_realname}-c-${pkgver}"
  patch -p1 -i ${srcdir}/0012-mingw-pwd.patch
#  patch -p1 -i ${srcdir}/0001-mingw-cmake.patch
#  patch -p1 -i ${srcdir}/0003-ncconfigure-strlcat-export.patch
#  patch -p1 -i ${srcdir}/0006-cvt-conversion.patch
#  patch -p1 -i ${srcdir}/0007-mingw-config.patch
}

build() {
  [[ -d ${srcdir}/build-static-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-static-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-static-${MINGW_CHOST} && cd ${srcdir}/build-static-${MINGW_CHOST}

  ${MINGW_PREFIX}/bin/cmake \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_EXE_LINKER_FLAGS="-Wl,--allow-multiple-definition" \
    -DBUILD_SHARED_LIBS=OFF \
    -DENABLE_DLL=OFF \
    -DENABLE_DAP=ON \
    -DENABLE_TESTS=ON \
    -DENABLE_NETCDF_4=ON \
    "${srcdir}/${_realname}-c-${pkgver}"
  make

  [[ -d ${srcdir}/build-shared-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-shared-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-shared-${MINGW_CHOST} && cd ${srcdir}/build-shared-${MINGW_CHOST}

  ${MINGW_PREFIX}/bin/cmake \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DENABLE_DAP=ON \
    -DENABLE_TESTS=OFF \
    -DENABLE_NETCDF_4=ON \
    -DENABLE_LOGGING=ON \
    "${srcdir}/${_realname}-c-${pkgver}"
  make
}

check() {
  cd ${srcdir}/build-static-${MINGW_CHOST}
  make test
}

package() {
  cd ${srcdir}/build-static-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  cd ${srcdir}/build-shared-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${_realname}-c-${pkgver}/COPYRIGHT \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYRIGHT

  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  local SRCDIR_WIN=$(cygpath -wm ${srcdir})
  for _f in "${pkgdir}${MINGW_PREFIX}"/lib/cmake/netCDF/*.cmake; do
    sed -e "s|${PREFIX_WIN}|\$\{_IMPORT_PREFIX\}|g" \
        -e "s|${SRCDIR_WIN}|.|" -i ${_f}
  done

  for pcfile in "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/*.pc; do
    sed -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
        -e "s|${SRCDIR_WIN}|.|" -i "${pcfile}"
  done

  sed -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
      -e "s|${SRCDIR_WIN}|.|" -i "${pkgdir}${MINGW_PREFIX}/bin/nc-config"
  sed -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
      -e "s|${SRCDIR_WIN}|.|" -i "${pkgdir}${MINGW_PREFIX}/lib/libnetcdf.settings"
}
