# Contributor: Robert T. McGibbon <rmcgibbo@gmail.com>

_realname=netcdf
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.9.0
pkgrel=2
pkgdesc="Interface for scientific data access to large binary data (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('custom')
url="https://www.unidata.ucar.edu/software/netcdf/"
depends=("${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-hdf5"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             autoconf
             automake
             libtool)
checkdepends=("unzip")
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/Unidata/netcdf-c/archive/v${pkgver}.tar.gz"
	0002-no-file.patch)
sha256sums=('9f4cb864f3ab54adb75409984c6202323d2fc66c003e5308f3cdf224ed41c0a6'
	'd6f566945c232f78cce680ddc235c178fc1c5a41e5b8eee067f5b9251e2ea81e')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}/${_realname}-c-${pkgver}"

  # Patch from https://github.com/msys2/MINGW-packages/pull/11920:
  apply_patch_with_msg 0002-no-file.patch

  autoreconf -if
}

build() {
  cd "${srcdir}/${_realname}-c-${pkgver}"

  # nczarr tests fail for i686 in Rtools40:
  if [[ ${CARCH} == "i686" ]]; then
    nczarr_opt=--disable-nczarr
  else
    nczarr_opt=""
  fi

  PREFIX_WIN="$( cygpath -wm / )"
  CPPFLAGS="$( pkg-config --cflags libxml-2.0 libcurl |
	  sed "s|${PREFIX_WIN}|/|g" ) -D__USE_MINGW_ANSI_STDIO=1"
  LIBS="$( pkg-config --libs-only-l --static libxml-2.0 libcurl )"

  ./configure \
    --prefix=${MINGW_PREFIX} \
    --disable-shared \
    --enable-static \
    --enable-hdf5 \
    --enable-dap \
    --enable-libxml2 \
    --disable-plugins \
    --disable-byterange \
    --disable-logging \
    --enable-dap-remote-tests \
    ${nczarr_opt} \
    CPPFLAGS="${CPPFLAGS}" \
    LIBS="${LIBS}"

  make ${MAKEFLAGS}
}

check() {
  cd "${srcdir}/${_realname}-c-${pkgver}"
  make check ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${_realname}-c-${pkgver}"
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${_realname}-c-${pkgver}/COPYRIGHT \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYRIGHT

  sed -e "s|${srcdir}|.|" -i "${pkgdir}${MINGW_PREFIX}/lib/libnetcdf.settings"
}
